# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/update/sys_installer/sys_installer_default_cfg.gni")
import("//build/ohos.gni")

sys_installer_path = rebase_path("${sys_installer_absolutely_path}", ".")
config("utils_exported_header") {
  visibility = [ ":*" ]

  include_dirs = [
    "${sys_installer_path}/frameworks/installer_manager/include",
    "${sys_installer_path}/interfaces/innerkits",
    "${sys_installer_path}/interfaces/inner_api/include",
    "${sys_installer_path}/services/module_update/util/include",
  ]
}

ohos_shared_library("module_update_utils") {
  sources = [
    "${sys_installer_path}/services/module_update/util/src/module_file.cpp",
    "${sys_installer_path}/services/module_update/util/src/module_update_verify.cpp",
    "${sys_installer_path}/services/module_update/util/src/module_utils.cpp",
    "${sys_installer_path}/services/module_update/util/src/module_zip_helper.cpp",
  ]

  public_configs = [ ":utils_exported_header" ]

  public_external_deps = [
    "cJSON:cjson",
    "zlib:shared_libz",
  ]

  deps = []

  external_deps = [
    "bounds_checking_function:libsec_shared",
    "cJSON:cjson",
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_single",
    "openssl:libcrypto_shared",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "updater:libdiff_patch_shared",
    "updater:libmiscinfo",
    "updater:libpackage_shared",
    "updater:libringbuffer",
    "updater:libupdaterlog_shared",
    "updater:libupdaterpackage_shared",
    "updater:libutils",
  ]
  if (ohos_indep_compiler_enable) {
    external_deps += [
      "init:libfsmanager_static",
      "updater:libfsmanager",
      "selinux_adapter:librestorecon",
    ]
  }

  if (defined(global_parts_info.startup_hvb)) {
    sources += [
      "${sys_installer_path}/services/module_update/util/src/module_hvb_ops.cpp",
      "${sys_installer_path}/services/module_update/util/src/module_hvb_utils.cpp",
    ]

    defines = [ "SUPPORT_HVB" ]

    external_deps += [
      "hvb:libhvb_static",
      "init:libfsmanager_static_real",
    ]
  }

  part_name = "sys_installer"
  subsystem_name = "updater"
}
